---
layout: post
title: Locale parser with fparsec
date: 2013-07-07 08:00:22.000000000 -07:00
type: post
parent_id: '0'
published: true
password: ''
status: publish
categories:
- Code
tags:
- fparsec
- fsharp
- language implementation
- parser
meta:
  _edit_last: '1'
  _syntaxhighlighter_encoded: '1'
  _su_rich_snippet_type: none
  _wpas_done_all: '1'
  _jetpack_related_posts_cache: a:1:{s:32:"8f6677c9d6b0f903e98ad32ec61f8deb";a:2:{s:7:"expires";i:1554392642;s:7:"payload";a:3:{i:0;a:1:{s:2:"id";i:3723;}i:1;a:1:{s:2:"id";i:4131;}i:2;a:1:{s:2:"id";i:4077;}}}}
author:
  login: akropp
  email: akropp@gmail.com
  display_name: akropp
  first_name: ''
  last_name: ''
permalink: "/2013/07/07/locale-parser-fparsec/"
---
<p>Localizing an application consists of extracting out user directed text and managing it outside of hardcoded strings in your code. This lets you tweak strings without having to recompile, and if done properly, allows you to support multiple languages. Localizing is no easy task, it messes up spacing, formatting, name/date other cultural information, but thats a separate issue. The crux of localizing is text.  </p>
<p>But, who just uses bare text to display things to the user? Usually you want to have text be a little dynamic.  Something like </p>
<p>[code]<br />
Hello {user}! Welcome!<br />
[/code]</p>
<p>Here, <code>user</code> will be some sort of dynamic property.  To support this, your locale files need a way to handle arguments.  </p>
<p>One way of storing contents in a locale file is like this:</p>
<p>[code]<br />
ExampleText = Some Text {argName:argType} other text etc<br />
            = This is on a seperate newline<br />
UserLoginText = ...<br />
[/code]</p>
<p>This consists of an identifier, followed by an equals sign, followed by some text with arguments of the form {x:y}.  To make a new line you have a new line with an equals sign and you continue your text.  When you reach a string with an identifier, you have a new locale element.</p>
<p>But you can also have comments, like </p>
<p>[code]<br />
# this is a comment, ignore me!<br />
[/code]</p>
<p>And to throw a monkey wrench in the problem, you can also have arguments with no types, of the form <code>{argName}</code>.</p>
<p>The end goal, is to be able to reference your locale contents in code, something like</p>
<p>[code]<br />
Locale.ExampleText (&quot;foo&quot;);<br />
[/code]</p>
<p>But to get to the point where you can reference this you need to translate your locale files into something workable, kind of like a syntax tree.  If you have a working syntax tree of your locale files you can generate strongly typed locale code for you to use in your application.</p>
<h2>The data</h2>
<p>To parse a locale file of this format I used fparsec. One reason was that it already handles lookaheads and backtracking, and another reason is that I wanted to play with it :)</p>
<p>Going with a data first design, I thought about what I wanted to my final output to be and came up with 3 discriminated unions that look like this:</p>
<p>[fsharp]<br />
type Arg =<br />
    | WithType of string * string<br />
    | NoType of string</p>
<p>type LocaleContents =<br />
    | Argument of Arg<br />
    | Text of string<br />
    | Line of LocaleContents list<br />
    | Comment of string<br />
    | NewLine</p>
<p>type Locale =<br />
    | Entry of string * LocaleContents<br />
    | IgnoreEntry of LocaleContents<br />
[/fsharp]</p>
<h2>Utilities</h2>
<p>The next step was to build out some common utilities that I can use. I knew I'd need to be able to parse a phrase, a single word, and know when things are between brackets:</p>
<p>[fsharp]<br />
(*<br />
    Utilities<br />
*)</p>
<p>let brackets = isAnyOf ['{';'}']</p>
<p> (* non new line space *)<br />
let regSpace = manySatisfy (isAnyOf [' ';'\t'])</p>
<p>(* any string literal that is charcaters *)<br />
let phrase = many1Chars (satisfy (isNoneOf ['{';'\n']))</p>
<p>let singleWord = many1Chars (satisfy isDigit &lt;|&gt; satisfy isLetter &lt;|&gt; satisfy (isAnyOf ['_';'-']))</p>
<p>(* utility method to set between parsers space agnostic *)<br />
let between x y p = pstring x &gt;&gt;. regSpace &gt;&gt;. p .&gt;&gt; regSpace .&gt;&gt; pstring y<br />
[/fsharp]</p>
<p>Fparsec comes with a lot of great functions and parser combinators to create robust parsers. The idea is to combine parser functions from smaller parsers into larger parsers.  I liked working with it because it felt like dealing directly with a grammar.</p>
<h2>Arguments</h2>
<p>Now that I was able to parse words, phrases, and I could seperate out newlines from spaces, lets tackle an argument:</p>
<p>[fsharp]<br />
(*<br />
    Arguments of {a:b} or {a}<br />
*)</p>
<p>let argDelim = pstring &quot;:&quot;</p>
<p>let argumentNoType = singleWord |&gt;&gt; NoType</p>
<p>let argumentWithType = singleWord .&gt;&gt;.? (argDelim &gt;&gt;. singleWord) |&gt;&gt; WithType</p>
<p>let arg = (argumentWithType &lt;|&gt; argumentNoType) |&gt; between &quot;{&quot; &quot;}&quot; |&gt;&gt; Argument<br />
[/fsharp]</p>
<p>The <code>.>>.?</code> combinator says to apply both combinators results as a tuple, but if it fails to backtrack to the state of the previous parser.  Also, the <code><|></code> combinator lets you apply parsers as alternatives, so either of the parsers can be applied.</p>
<h2>Text elements</h2>
<p>Next up is text elements. This is the contents after the = of the identifier, but not including arguments.  For example, if our locale entry is</p>
<p>[code]<br />
UserLogin = Hey! Whats up?<br />
          = new lineezzz<br />
[/code]</p>
<p>We want to match on "Hey! Whats up?", followed by an explict newline, followed by "new lineeezz"</p>
<p>[fsharp]<br />
(*<br />
    Text Elements<br />
*)</p>
<p>let textElement = phrase |&gt;&gt; Text</p>
<p>let newLine = (unicodeNewline &gt;&gt;? regSpace &gt;&gt;? pstring &quot;=&quot;) &gt;&gt;% NewLine</p>
<p>let line = many (regSpace &gt;&gt;? (arg &lt;|&gt; textElement &lt;|&gt; newLine)) |&gt;&gt; Line<br />
[/fsharp]</p>
<p>Remembering that a phrase is any text except for a start bracket and a newline, we can parse all text up to an argument.  New lines are a new line, followed by some space (maybe), followed by an equal sign.  Since the newline doesn't contain any data we care about from the parser we can ignore the output and just assign the result to the union type <code>NewLine</code> using the <code>>>%</code> operator.  </p>
<p>But a line is an aggregation of new lines, arguments, and phrases, so we can use the fparsec <code>many</code> operator, along with the 3 alternatives (arguments, text elements, and new lines) to build out an actual line.  </p>
<h2>An Entry</h2>
<p>Since we have arguments, new lines, and text set up, we can finally put it all together.  What I need now is to match when we have an identifier ("UserLogin"), an equals sign, followed by a line.</p>
<p>[fsharp]<br />
(*<br />
    Entries<br />
*)</p>
<p>let delim = regSpace &gt;&gt;. pstring &quot;=&quot; .&gt;&gt; regSpace</p>
<p>let identifier = regSpace &gt;&gt;. singleWord .&gt;&gt; delim</p>
<p>let localeElement = unicodeSpaces &gt;&gt;? (identifier .&gt;&gt;. line .&gt;&gt; skipRestOfLine true) |&gt;&gt; Entry<br />
[/fsharp]</p>
<p>This gives you a tuple of identifier * line, representing your entire locale element.</p>
<h2>Comments</h2>
<p>But we also have to account for comments.  Thankfully those are pretty easy</p>
<p>[fsharp]<br />
(*<br />
    Comments<br />
*)</p>
<p>let comment = pstring &quot;#&quot; &gt;&gt;. restOfLine false |&gt;&gt; Comment</p>
<p>let commentElement = unicodeSpaces &gt;&gt;? comment |&gt;&gt; IgnoreEntry<br />
[/fsharp]</p>
<p>This says if you match a "#" then take the rest of the line (but leave the newline since other parsers will handle that).  We might as well maintain the comment information so we can pipe that result to the <code>IgnoreEntry</code> union type.</p>
<h2>Running the parser</h2>
<p>And now we just have to piece together comments, locale elements, and run the parser</p>
<p>[fsharp]<br />
(*<br />
    Full Locale<br />
*)</p>
<p>let locale = many (commentElement &lt;|&gt; localeElement) .&gt;&gt; eof</p>
<p>let test input = match run locale input with<br />
                    | Success(r,_,_) -&gt; r<br />
                    | Failure(r,_,_) -&gt;<br />
                            Console.WriteLine r<br />
                            raise (Error(r))<br />
[/fsharp]</p>
<h2>Example</h2>
<p>Lets try it out. Here is my sample locale:</p>
<p>[code]<br />
UserLogin = {user}! Whats up!<br />
		  = You rock, thanks for logging</p>
<p>UserLogout = {firstName:string}, {lastName:string}...why you gotta go? We were just getting to know you<br />
[/code]</p>
<p>And running it in fsi</p>
<p>[fsharp]<br />
&gt; test &quot;UserLogin = {user}! Whats up!<br />
		  = You rock, thanks for logging</p>
<p>UserLogout = {firstName:string}, {lastName:string}...why you gotta go? We were just getting to know you 		 &quot;;;</p>
<p>val it : Locale list =<br />
  [Entry (&quot;UserLogin&quot;, Line [Argument (NoType &quot;user&quot;); Text &quot;! Whats up!&quot;; NewLine; Text &quot;You rock, thanks for logging&quot;]);<br />
   Entry (&quot;UserLogout&quot;, Line<br />
        [Argument (WithType (&quot;firstName&quot;,&quot;string&quot;)); Text &quot;, &quot;;<br />
         Argument (WithType (&quot;lastName&quot;,&quot;string&quot;));<br />
         Text &quot;...why you gotta go? We were just getting to know you 		 &quot;])]<br />
[/fsharp]</p>
<p>And now its easy to iterate and manipulate the data! </p>
<h2>Source</h2>
<p>Full source available at <a href="https://github.com/devshorts/Locale-parser/blob/master/FParsecCombinators/Program.fs">my github</a></p>
